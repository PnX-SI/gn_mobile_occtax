// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.1.1"
        // NOTE: Do not place application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.jlleitschuh.gradle.ktlint-idea"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

File getVersionPropertiesFile(moduleName) {
    def versionPropsFile = file(moduleName + '/version.properties')

    println("getVersionCode: loading '" + versionPropsFile + "'")

    if (!versionPropsFile.exists()) {
        versionPropsFile.createNewFile()
    }

    return versionPropsFile
}

Properties readVersionProperties(moduleName) {
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(getVersionPropertiesFile(moduleName)))

    if (!versionProps['VERSION_CODE']) {
        versionProps['VERSION_CODE'] = "1"
    }

    return versionProps
}

int getVersionCode(moduleName) {
    Properties versionProps = readVersionProperties(moduleName)

    return Integer.parseInt(versionProps['VERSION_CODE'].toString()).intValue()
}

@SuppressWarnings("unused")
int updateVersionCode(moduleName) {
    Properties versionProps = readVersionProperties(moduleName)
    def versionCode = getVersionCode(moduleName) + 1

    versionProps['VERSION_CODE'] = versionCode.toString()
    versionProps.store(getVersionPropertiesFile(moduleName).newWriter(), null)

    println("updateVersionCode: " + versionCode)

    return versionCode
}