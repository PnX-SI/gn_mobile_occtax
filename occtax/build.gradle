plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.dagger.hilt.android)
    alias(libs.plugins.kapt)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
}

android {

    namespace 'fr.geonature.occtax'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    defaultConfig {
        applicationId "fr.geonature.occtax2"
        compileSdk 34
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 3310
        versionName "2.7.0"
        buildConfigField "String", "BUILD_DATE", "\"" + new Date().getTime() + "\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = project.name + "-" + versionName
    }

    signingConfigs {
        release {
            enableV3Signing true
            readKeystoreProperties(project.name)
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEV"
        }

        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':commons')
    implementation project(':datasync')
    implementation project(':viewpager')
    implementation project(':maps')

    // Hilt dependencies
    implementation libs.androidx.hilt.work
    implementation libs.hilt.android
    kapt libs.androidx.hilt.compiler
    kapt libs.hilt.android.compiler

    implementation libs.androidx.cardview
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.recyclerview
    implementation libs.material
    implementation libs.androidx.work.runtime.ktx
    implementation libs.fastscroll

    // Logging
    implementation libs.tinylog.api.kotlin
    runtimeOnly libs.tinylog.impl

    // Testing dependencies
    testImplementation libs.androidx.core.testing
    testImplementation libs.androidx.junit.ktx
    testImplementation libs.androidx.work.testing
    testImplementation(libs.mockwebserver)
    testImplementation libs.mockk
    testImplementation libs.mockk.agent.jvm
    testImplementation libs.junit
    testImplementation libs.kotlinx.coroutines.test
    testImplementation libs.robolectric
    testImplementation libs.slf4j.tinylog
}
