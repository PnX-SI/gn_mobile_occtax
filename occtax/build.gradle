plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

version = "2.0.0"

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    defaultConfig {
        applicationId "fr.geonature.occtax2"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode updateVersionCode(project.name)
        versionName version
        buildConfigField "String", "BUILD_DATE", "\"" + new Date().getTime() + "\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = project.name + "-" + versionName
    }

    signingConfigs {
        release {
            readKeystoreProperties(project.name)
        }
    }

    buildTypes {
        debug {
            versionNameSuffix ".${defaultConfig.versionCode}-DEV"
        }

        release {
            signingConfig signingConfigs.release
            versionNameSuffix ".${defaultConfig.versionCode}"
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        generic {}
        pag {}
        pnc {}
        pne {}
        pnm {}
        pnv {}
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':commons')
    implementation project(':datasync')
    implementation project(':viewpager')
    implementation project(':maps')

    // Hilt dependencies
    implementation "androidx.hilt:hilt-work:$androidx_hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:$androidx_hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "io.github.l4digital:fastscroll:2.0.1"

    // Logging
    implementation "org.tinylog:tinylog-api-kotlin:$tinylog_version"
    runtimeOnly "org.tinylog:tinylog-impl:$tinylog_version"

    // Testing dependencies
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
}
